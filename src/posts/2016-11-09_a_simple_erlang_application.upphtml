<!--
A Simple Erlang Application
November 9, 2016
erlang
-->
    <section>
    
        <p>An HTTP server structured as an Erlang application.  Built with Elli, it uses ERL_LIBS and make instead of rebar.
    
    	<figure>
        <img src='/img/full/service.png' alt='Luminous box with the word service on it in block capitals.' class='webfeedsFeaturedVisual'/>
    	<figcaption>
           &copy; 2016 <a href='/imginfo/service.html'>Mike Wilson</a>
    	  for Unsplash
    	</figcaption>
    	</figure>
    
    </section>
    <section>
    
    <h2>The Code</h2>
    
    <h3>Directory Structure</h3>
    
            <pre><code>
    .
    ├── Makefile
    ├── ebin
    └── src
        ├── erlsrv.app.src
        ├── erlsrv.erl
        ├── es_callback.erl
        └── es_sup.erl
    </code></pre>
    
    <p>The Erlang <a href="http://erlang.org/doc/design_principles/applications.html">documentation</a> mentions four directories (src, ebin, priv and include) but the application started fine with just two.
    
    <h3>Makefile</h3>
    
            <pre><code>
    all: ebin/es_sup.beam ebin/es_callback.beam ebin/erlsrv.app ebin/erlsrv.beam
    
    ELLI=${HOME}/src/elli
    ${ELLI}/ebin:
    	(cd ${ELLI} ; make)
    
    ebin/%.beam: src/%.erl ${ELLI}/ebin
    	ERL_LIBS=${ELLI} erlc -o ebin/ $<
    
    ebin/erlsrv.app: src/erlsrv.app.src
    	cp $? $@
    
    .PHONY: clean
    clean:
    	rm -f ebin/*
    </code></pre>
    
    <p><code>ERL_LIBS</code> makes this work.  I cloned the <a href="https://github.com/knutin/elli">Elli</a> repository under <code>$HOME/src</code> and set <code>ERL_LIBS</code> to that before calling <code>erlc</code>.  A simple way to manage dependencies that is built in to Erlang.
    
    
            <blockquote>
    
    <p>Environment variable ERL_LIBS (defined in the operating system) can be used to define more library directories to be handled in the same way as the standard OTP library directory described above, except that directories without an ebin directory are ignored.
    
    <p>All application directories found in the additional directories appears before the standard OTP applications, except for the Kernel and STDLIB applications, which are placed before any additional applications. In other words, modules found in any of the additional library directories override modules with the same name in OTP, except for modules in Kernel and STDLIB.
    
    <p>Environment variable ERL_LIBS (if defined) is to contain a colon-separated (for Unix-like systems) or semicolon-separated (for Windows) list of additional libraries.
    
                <footer>Ericsson AB. <cite><a href='http://erlang.org/doc/man/code.html'>erlang.org code</a></cite> (retrieved November 6, 2016)</footer>
            </blockquote>
    
    <h3>src/erlsrv.app.src</h3>
    
            <pre><code>
    {application, erlsrv, [{mod, {erlsrv,[]}}]}.
    </code></pre>
    
    <p>The smallest possible application configuration file.
    
    <h3>src/erlsrv.erl</h3>
    
            <pre><code>
    -module(erlsrv).
    -behavior(application).
    
    -export([start/2, stop/1]).
    
    start(_Type, _Args) -> es_sup:start_link().
    
    stop(_State) -> ok.
    </code></pre>
    
    <p>The job of the application behavior is to start the main supervisor.
    
    
    <h3>src/es_callback.erl</h3>
    
            <pre><code>
    -module(es_callback).
    -export([handle/2, handle_event/3]).
    
    -include_lib("elli/include/elli.hrl").
    -behaviour(elli_handler).
    
    % Dispatch to handler functions
    handle(Req, _Args) ->
        handle(Req#req.method, elli_request:path(Req), Req).
    
    handle(&rsquo;GET&rsquo;,[<<"hello">>, <<"world">>], _Req) ->
        {ok, [], <<"Hello World!">>};
    
    handle(_, _, _Req) ->
        {404, [], <<"Not Found">>}.
    
    %% @doc: Handle request events, like request completed, exception
    %% thrown, client timeout, etc. Must return &rsquo;ok&rsquo;.
    handle_event(_Event, _Data, _Args) ->
        ok.
    </code></pre>
    
    <p>Dispatch HTTP calls to handlers.  Elli spawns a process for each request so they are isolated from each other.
    <p>This is the file that will grow as I build a server that does something.
    
    
    <h3>src/es_sup.erl</h3>
    
            <pre><code>
    -module(es_sup).
    -behaviour(supervisor).
    -export([start_link/0]).
    -export([init/1]).
    
    start_link() ->
        supervisor:start_link({local, ?MODULE}, ?MODULE, []).
    
    init([]) ->
        ElliOpts = [{callback, es_callback}, {port, 3000}],
        ElliSpec = {
            es_http,
            {elli, start_link, [ElliOpts]},
            permanent,
            5000,
            worker,
            [elli]},
    
        {ok, { {one_for_one, 5, 10}, [ElliSpec]} }.
    </code></pre>
    
    <p>The supervisor for Elli.  Copied right from Elli&rsquo;s <a href="https://github.com/knutin/elli">github readme</a>.
    
    <h2>Running the application</h2>
    
    
            <pre><code>
    $ ERL_LIBS=$HOME/src/elli erl -pa ebin
    Erlang/OTP 19 [erts-8.0.1] [source-ca40008] [64-bit] [smp:8:8] [async-threads:10] [hipe] [kernel-poll:false]
    
    Eshell V8.0.1  (abort with ^G)
    1> application:start(erlsrv).
    ok
    2>
    </code></pre>
    
    <p>Note the use of ERL_LIBS when starting interpreter.
    
            <pre><code>
    $ curl -v http://127.0.0.1:3000/hello/world
    *   Trying 127.0.0.1&hellip;
    * Connected to 127.0.0.1 (127.0.0.1) port 3000 (#0)
    > GET /hello/world HTTP/1.1
    > Host: 127.0.0.1:3000
    > User-Agent: curl/7.49.1
    > Accept: */*
    > 
    < HTTP/1.1 200 OK
    < Connection: Keep-Alive
    < Content-Length: 12
    < 
    * Connection #0 to host 127.0.0.1 left intact
    Hello World!$
    </code></pre>
    
    <h2>Notes</h2>
    
    <h3>My Goals</h3>
    <ol>
    <li>simplest code possible</li>
    <li>take my time and understand each step</li>
    <li>support SSL</li>
    <li>support basic auth</li>
    </ol>
    
    <h3>Why no rebar?</h3>
    
            <pre><code>
    $ cd $HOME/src/rebar3/
    $ du -sh src
    692K	src
    $ 
    </code></pre>
    
    <p>Violates goal #1.
    
    <h3>Surprising behavior when no mod in application configuration file.</h3>
    
    <p>Don&rsquo;t leave out the <code>mod</code> parameter in the app configuration file.
    
    <p>For example, if I change the config file to:
    
            <pre><code>
    {application, erlsrv, []}.
    </code></pre>
    
    <p>I can start the application without error
    
            <pre><code>
    $ ERL_LIBS=$HOME/src/elll erl -pa ebin
    Erlang/OTP 19 [erts-8.0.1] [source-ca40008] [64-bit] [smp:8:8] [async-threads:10] [hipe] [kernel-poll:false]
    
    Eshell V8.0.1  (abort with ^G)
    1> application:start(erlsrv).
    ok
    2> application:info().
    </code></pre>
    
    <p>and the application is loaded
    
            <pre><code>
    2> application:which_applications().
    [{erlsrv,[],[]},
     {stdlib,"ERTS  CXC 138 10","3.0"},
     {kernel,"ERTS  CXC 138 10","5.0"}]
    3> 
    </code></pre>
    
    <p>and shows as running
    
    
            <pre><code>
    3> application:info().
    [{loaded,[{kernel,"ERTS  CXC 138 10","5.0"},
              {stdlib,"ERTS  CXC 138 10","3.0"},
              {erlsrv,[],[]}]},
     {loading,[]},
     {started,[{erlsrv,temporary},
               {stdlib,permanent},
               {kernel,permanent}]},
     {start_p_false,[]},
     {running,[{erlsrv,undefined},
               {stdlib,undefined},
               {kernel,<0.33.0>}]},
     {starting,[]}]
    4> 
    
    </code></pre>
    
    <p>(There is supposed to be a process ID instead of the atom <code>undefined</code> on the line <code>{running,[{erlsrv,undefined}</code>.)
    
    <p>Hitting the server with curl produces Connection refused:
    
            <pre><code>
    $ curl -v http://127.0.0.1:3000/hello/world
    *   Trying 127.0.0.1&hellip;
    * connect to 127.0.0.1 port 3000 failed: Connection refused
    * Failed to connect to 127.0.0.1 port 3000: Connection refused
    * Closing connection 0
    curl: (7) Failed to connect to 127.0.0.1 port 3000: Connection refused
    $ 
    </code></pre>
    
    <p>What&rsquo;s happening is that the the <code>mod</code> argument tells the application behavior what module holds the <code>start/2</code> method to call.  If we don&rsquo;t provide that module name, the behavior does not call any start method and the server is not started.
    
    <h3>Why Elli?</h3>
    <p>Tried mochiweb, but I was too new to Erlang.
    <p>Tried yaws and while creating an app was easy, but I got confused by yapp and embedded options.
    <p>Tried cowboy (or maybe it was Elixer?), but the tutorial didn&rsquo;t work for me (and I gave up quickly).
    <p>By the time I found Elli, I was more familiar with Erlang.  Also, the github readme had exactly the code I needed.</li>
    <p>Also, it&rsquo;s lightweight:
    
            <pre><code>
    $ cd $HOME/src/elli
    $ du -sh src
     92K	src
    $ 
    </code></pre>
    
    
    
    <h3>Next steps &hellip;</h3>
    
    <p>This server is lacking pretty basic functionality.  Like logging.  Or including a Date HTTP header in the response.
    
    <p>Some future steps, in no particular order:
    
    <ul>
    <li>hot code reload</li>
    <li>build a release</li>
    <li>wrap into command-line with escript or sh</li>
    <li>add logging and basic auth</li>
    </ul>
    
